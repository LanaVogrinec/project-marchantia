# Main entrypoint of the workflow.
# Please follow the best practices:
# https://snakemake.readthedocs.io/en/stable/snakefiles/best_practices.html,
# in particular regarding the standardized folder structure mentioned there.

# conda activate tobamo-snakemake
# snakemake --use-conda
# snakemake --use-conda -c40 -k -p


configfile: "config/config.yaml"


wildcard_constraints:
    accession="ERR\d+|SRR\d+|DRR\d+",


ruleorder: spades_isolate_se > megahit_se
ruleorder: spades_isolate_pe > megahit_pe


rule all:
    input:
        expand(
            "results/{accession}/09_{accession}_megan6_results.csv",
            accession=config["SRRs"],
        ),


rule download_SRA:
    output:
        fastq="resources/SRA/{accession}",
    log:
        "logs/resources/SRA/{accession}.download.log",
    conda:
        "envs/fasterq-dump.yaml"
    threads: workflow.cores
    shell:
        """
        ./workflow/scripts/download_sra.sh {output.fastq}
        """


rule trim_pe:
    input:
        r1="resources/SRA/{accession}_1.fastq.gz",
        r2="resources/SRA/{accession}_2.fastq.gz",
        iclip="resources/TruSeq3-PE.fa",
    output:
        tp1="results/{accession}/01_{accession}_trim_1_paired.fq.gz",
        tp2="results/{accession}/01_{accession}_trim_2_paired.fq.gz",
        tup1="results/{accession}/01_{accession}_trim_1_unpaired.fq.gz",
        tup2="results/{accession}/01_{accession}_trim_2_unpaired.fq.gz",
        check="results/{accession}/01_{accession}_trim_pe.done",
    log:
        logO="results/{accession}/01_{accession}_trim_pe.log",
        logE="results/{accession}/01_{accession}_trim_pe_err.log",
    conda:
        "envs/trimmomatic.yaml"
    threads: 4
    shell:
        """
        trimmomatic PE -threads {threads} -phred33 {input.r1} {input.r2} {output.tp1} {output.tup1} {output.tp2} {output.tup2} ILLUMINACLIP:{input.iclip}:2:30:10:2:True LEADING:3 TRAILING:3 MINLEN:36 > {log.logO} 2> {log.logE}
        touch {output.check}
        """


rule trim_se:
    input:
        r="resources/SRA/{accession}.fastq.gz",
        iclip="resources/TruSeq3-SE.fa",
    output:
        tp="results/{accession}/01_{accession}_trim_single.fq.gz",
        check="results/{accession}/01_{accession}_trim_se.done",
    log:
        logO="results/{accession}/01_{accession}_trim_se.log",
        logE="results/{accession}/01_{accession}_trim_se_err.log",
    conda:
        "envs/trimmomatic.yaml"
    threads: 4
    shell:
        """
        trimmomatic SE -threads {threads} -phred33 {input.r} {output.tp} ILLUMINACLIP:{input.iclip}:2:30:10:2:True LEADING:3 TRAILING:3 MINLEN:36 > {log.logO} 2> {log.logE}
        touch {output.check}
        """


rule spades_isolate_pe:
    input:
        tp1="results/{accession}/01_{accession}_trim_1_paired.fq.gz",
        tp2="results/{accession}/01_{accession}_trim_2_paired.fq.gz",
    output:
        d=temp(directory("results/{accession}/{accession}_spades_isolate")),
        f="results/{accession}/02_{accession}_spades_isolate_contigs.fasta",
    log:
        logO="results/{accession}/02_{accession}_spades_isolate_pe.log",
        logE="results/{accession}/02_{accession}_spades_isolate_pe_err.log",
    benchmark:
        "results/{accession}/02_{accession}_benchmark_spades_isolate_pe.txt"
    conda:
        "envs/spades.yaml"
    threads: 8
    shell:
        """
        rm -Rf {output.d}
        if [ -e "results/{wildcards.accession}/02_{wildcards.accession}_spades_isolate_pe_contigs.fasta.no_isolate" ]; then
            mkdir -p results/{wildcards.accession}/{wildcards.accession}_spades_isolate
            cp -f resources/empty.fasta results/{wildcards.accession}/{wildcards.accession}_spades_isolate/contigs.fasta
        else
            set +e
            spades.py --isolate --threads {threads} -1 {input.tp1} -2 {input.tp2} -o {output.d} > {log.logO} 2> {log.logE}
            exitcode=$?
            set -e
            if [ $exitcode -ne 0 ]; then
                mkdir -p results/{wildcards.accession}/{wildcards.accession}_spades_isolate
                touch "results/{wildcards.accession}/02_{wildcards.accession}_spades_isolate_pe_contigs.fasta.no_isolate"
                cp -f resources/empty.fasta results/{wildcards.accession}/{wildcards.accession}_spades_isolate/contigs.fasta
            fi
        fi
        cp -f results/{wildcards.accession}/{wildcards.accession}_spades_isolate/contigs.fasta {output.f}
        """


rule spades_isolate_se:
    input:
        tp="results/{accession}/01_{accession}_trim_single.fq.gz",
    output:
        d=temp(directory("results/{accession}/{accession}_spades_isolate")),
        f="results/{accession}/02_{accession}_spades_isolate_contigs.fasta",
    log:
        logO="results/{accession}/02_{accession}_spades_isolate_se.log",
        logE="results/{accession}/02_{accession}_spades_isolate_se_err.log",
    benchmark:
        "results/{accession}/02_{accession}_benchmark_spades_isolate_se"
    conda:
        "envs/spades.yaml"
    threads: 8
    shell:
        """
        rm -Rf {output.d}
        if [ -e "results/{wildcards.accession}/02_{wildcards.accession}_spades_isolate_se_contigs.fasta.no_isolate" ]; then
            mkdir -p results/{wildcards.accession}/{wildcards.accession}_spades_isolate
            cp -f resources/empty.fasta results/{wildcards.accession}/{wildcards.accession}_spades_isolate/contigs.fasta
        else
            set +e
            spades.py --isolate --threads {threads} -s {input.tp} -o {output.d} > {log.logO} 2> {log.logE}
            exitcode=$?
            set -e
            if [ $exitcode -ne 0 ]; then
                mkdir -p results/{wildcards.accession}/{wildcards.accession}_spades_isolate
                touch "results/{wildcards.accession}/02_{wildcards.accession}_spades_isolate_se_contigs.fasta.no_isolate"
                cp -f resources/empty.fasta results/{wildcards.accession}/{wildcards.accession}_spades_isolate/contigs.fasta
            fi
        fi
        cp -f results/{wildcards.accession}/{wildcards.accession}_spades_isolate/contigs.fasta {output.f}
        """


rule megahit_pe:
    input:
        r1="results/{accession}/01_{accession}_trim_1_paired.fq.gz",
        r2="results/{accession}/01_{accession}_trim_2_paired.fq.gz",
    output:
        d=temp(directory("results/{accession}/{accession}_megahit")),
        f="results/{accession}/02_{accession}_megahit_contigs.fasta",
    log:
        logO="results/{accession}/02_{accession}_megahit_pe.log",
        logE="results/{accession}/02_{accession}_megahit_pe_err.log",
    conda:
        "envs/megahit.yaml"
    threads: 2
    shell:
        """
        rm -Rf {output.d}
        if [ -e "results/{wildcards.accession}/02_{wildcards.accession}_spades_isolate_pe_contigs.fasta.no_isolate" ]; then
            if [ -e "results/{wildcards.accession}/02_{wildcards.accession}_megahit_contigs.fasta.no_megahit" ]; then
                mkdir -p results/{wildcards.accession}/{wildcards.accession}_megahit
                cp -f resources/empty.fasta results/{wildcards.accession}/{wildcards.accession}_megahit/final.contigs.fa
            else
                set +e
                megahit -t {threads} -1 {input.r1} -2 {input.r2} -o {output.d} > {log.logO} 2> {log.logE}
                exitcode=$?
                set -e
                if [ $exitcode -ne 0 ]; then
                    mkdir -p "results/{wildcards.accession}/{wildcards.accession}_megahit"
                    touch "results/{wildcards.accession}/02_{wildcards.accession}_megahit_contigs.fasta.no_megahit"
                    cp -f resources/empty.fasta "results/{wildcards.accession}/{wildcards.accession}_megahit/final.contigs.fa"
                fi
            fi
        else
            cp -f resources/empty.fasta "results/{wildcards.accession}/{wildcards.accession}_megahit/final.contigs.fa"
        fi
        cp results/{wildcards.accession}/{wildcards.accession}_megahit/final.contigs.fa {output.f}
        """


rule megahit_se:
    input:
        r="results/{accession}/01_{accession}_trim_single.fq.gz",
    output:
        d=temp(directory("results/{accession}/{accession}_megahit")),
        f="results/{accession}/02_{accession}_megahit_contigs.fasta",
    log:
        logO="results/{accession}/02_{accession}_megahit_se.log",
        logE="results/{accession}/02_{accession}_megahit_se_err.log",
    conda:
        "envs/megahit.yaml"
    threads: 2
    shell:
        """
        rm -Rf {output.d}
        if [ -e "results/{wildcards.accession}/02_{wildcards.accession}_spades_isolate_se_contigs.fasta.no_isolate" ]; then
            if [ -e "results/{wildcards.accession}/02_{wildcards.accession}_megahit_se_contigs.fasta.no_megahit" ]; then
                mkdir -p results/{wildcards.accession}/{wildcards.accession}_megahit
                cp -f resources/empty.fasta results/{wildcards.accession}/{wildcards.accession}_megahit/final.contigs.fa
            else
                set +e
                megahit -t {threads} -r {input.r} -o {output} > {log.logO} 2> {log.logE}
                exitcode=$?
                set -e
                if [ $exitcode -ne 0 ]; then
                    mkdir -p results/{wildcards.accession}/{wildcards.accession}_megahit
                    touch "results/{wildcards.accession}/02_{wildcards.accession}_megahit_se_contigs.fasta.no_megahit"
                    cp -f resources/empty.fasta results/{wildcards.accession}/{wildcards.accession}_megahit/final.contigs.fa
                fi
            fi
        cp -f resources/empty.fasta "results/{wildcards.accession}/{wildcards.accession}_megahit/final.contigs.fa"
        fi
        cp results/{wildcards.accession}/{wildcards.accession}_megahit/final.contigs.fa {output.f}
        """


rule unique_contigs:
    input:
        iso="results/{accession}/02_{accession}_spades_isolate_contigs.fasta",
        mega="results/{accession}/02_{accession}_megahit_contigs.fasta",
    output:
        comb="results/{accession}/03_{accession}_contigs_combined.fasta",
        out="results/{accession}/03_{accession}_contigs_unique.fasta",
    log:
        logO="results/{accession}/03_{accession}_contigs_unique.log",
        logE="results/{accession}/03_{accession}_contigs_unique_err.log",
    conda:
        "envs/biopython.yaml"
    threads: 1
    shell:
        """
        cat {input.iso} {input.mega} > {output.comb}
        python workflow/scripts/unique_contigs.py {output.comb} {output.out}  > {log.logO} 2> {log.logE}
        """


rule filter_contigs:
    input:
        "results/{accession}/03_{accession}_contigs_unique.fasta",
    output:
        add_accession="results/{accession}/04_{accession}_contigs_add_accession.fasta",
        filtered="results/{accession}/04_{accession}_contigs_filtered.fasta",
    log:
        logO="results/{accession}/04_{accession}_contigs_filter.log",
        logE="results/{accession}/04_{accession}_contigs_filter_err.log",
    conda:
        "envs/biopython.yaml"
    threads: 1
    shell:
        """
        python workflow/scripts/add_accession_to_fasta.py {input} {output.add_accession} {wildcards.accession} > {log.logO} 2> {log.logE}
        python workflow/scripts/filter_fasta_by_len.py {output.add_accession} {output.filtered} 600 8000 >> {log.logO} 2>> {log.logE}
        """


rule diamond_tpdb2:
    input:
        "results/{accession}/04_{accession}_contigs_filtered.fasta",
    output:
        "results/{accession}/05_{accession}_diamond_tpdb2.daa",
    log:
        logO="results/{accession}/05_{accession}_diamond_tpdb2.log",
        logE="results/{accession}/05_{accession}_diamond_tpdb2_err.log",
    benchmark:
        "results/{accession}/05_{accession}_benchmark_diamond_tpdb2.txt"
    conda:
        "envs/diamond-megan.yaml"
    threads: 2
    shell:
        """
        if [ -s {input} ]; then
            diamond blastx --threads 10 -d resources/tpdb2.dmnd -k 20 -q {input} --daa {output} > {log.logO} 2> {log.logE}
        else
            cp -f resources/empty.daa {output}
        fi
        """


rule diamond_nr:
    input:
        d="results/{accession}/05_{accession}_diamond_tpdb2.daa",
        f="results/{accession}/04_{accession}_contigs_filtered.fasta",
    output:
        info="results/{accession}/06_{accession}_diamond_info.tsv",
        selected="results/{accession}/06_{accession}_diamond_tpdb2_selected.fasta",
        out="results/{accession}/06_{accession}_diamond_nr.daa",
    log:
        logO="results/{accession}/06_{accession}_diamond_nr.log",
        logE="results/{accession}/06_{accession}_diamond_nr_err.log",
    benchmark:
        "results/{accession}/06_{accession}_benchmark_diamond_nr.txt"
    conda:
        "envs/diamond-megan.yaml"
    threads: 3
    shell:
        """
        diamond view --daa {input.d} --outfmt 6 > {output.info}
        python workflow/scripts/select_contigs.py {input.f} {output.info} {output.selected} > {log.logO} 2> {log.logE}
        if [ -s {output.info} ]; then
            diamond blastx -d resources/nr.dmnd -q {output.selected} --threads 10 -k 20 --unal 1 --daa {output.out} >> {log.logO} 2>> {log.logE}
        else
            cp -f {input.d} {output.out}
        fi
        """


rule meganizer:
    input:
        "results/{accession}/06_{accession}_diamond_nr.daa",
    output:
        "results/{accession}/07_{accession}_meganizer_tpdb2.daa",
    log:
        logO="results/{accession}/07_{accession}_meganizer_tpdb2.log",
        logE="results/{accession}/07_{accession}_meganizer_tpdb2_err.log",
    benchmark:
        "results/{accession}/07_{accession}_benchmark_meganizer_tpdb2.txt"
    conda:
        "envs/diamond-megan.yaml"
    threads: 3
    shell:
        """
        cp {input} {output}
        if [[ $(diamond view --daa {output} | wc -l) -ge 1 ]]; then
            daa-meganizer --threads {threads} -i {output} -mdb resources/megan-map-Feb2022.db -supp 0 > {log.logO} 2> {log.logE}
        fi
        """


rule megan_cli_export:
    input:
        "results/{accession}/07_{accession}_meganizer_tpdb2.daa",
    output:
        taxon="results/{accession}/08_{accession}_meganizer_tpdb2_read_classification.tsv",
        class_count="results/{accession}/08_{accession}_meganizer_tpdb2_class_count.tsv",
    log:
        logO="results/{accession}/08_{accession}_megan_export.log",
        logE="results/{accession}/08_{accession}_megan_export_err.log",
    conda:
        "envs/diamond-megan.yaml"
    shell:
        """
        daa2info -i {input} -r2c Taxonomy -p -o {output.taxon} > {log.logO} 2> {log.logE}
        daa2info -i {input} -c2c Taxonomy -p -o {output.class_count} >> {log.logO} 2>> {log.logE}
        """


rule megan6_concat:
    input:
        tsv="results/{accession}/08_{accession}_meganizer_tpdb2_read_classification.tsv",
        fasta="results/{accession}/06_{accession}_diamond_tpdb2_selected.fasta",
        info="results/{accession}/06_{accession}_diamond_info.tsv",
    output:
        "results/{accession}/09_{accession}_megan6_results.csv",
    log:
        logO="results/{accession}/09_{accession}_megan6_concat.log",
        logE="results/{accession}/09_{accession}_megan6_concat_err.log",
    conda:
        "envs/biopython.yaml"
    shell:
        """
        if [ -s {input.tsv} ] && [ -s {input.fasta} ] && [ -s {input.info} ]; then
            python workflow/scripts/megan6_concat.py {input.tsv} {input.fasta} {input.info} {output} > {log.logO} 2> {log.logE}
        else
            cp {input.tsv} {output}
        fi
        """


# python combine_results.py `ls results/*/*_megan6_results.csv` results/megan6_results_combined.csv
